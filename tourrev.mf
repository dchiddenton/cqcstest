clear echo

description

  Copyright by Bluebird Auto Rental Systems Ltd 2019.
  All rights reserved.

  Report: Tour Revenue Report
  Author: David Chiddenton
  $Revision: 2 $;
  
/*------------------------------------------------------------------------
Created: 4/9/2018
Modified:

Notes:
$Workfile: tourrev.mf $                              
$Archive: /AutoMate2/Version 5.0/Source/cqcs/tourrev.mf $
$Revision: 2 $
$History: tourrev.mf $
 * 
 * *****************  Version 2  *****************
 * User: Dchidden     Date: 9/12/19    Time: 11:52a
 * Updated in $/AutoMate2/Version 5.0/Source/cqcs
 * Add revision number to view in About screen
 * 
 * *****************  Version 1  *****************
 * User: Dchidden     Date: 9/11/19    Time: 1:28p
 * Created in $/AutoMate2/Version 5.0/Source/cqcs
 * Initial creation
------------------------------------------------------------------------*/

/*------------------------------------------------------------------------
 Retrieve standard parameters and create custom parameters
------------------------------------------------------------------------*/

run/parameterfile parameters.mf

--where tour:dealership = 0  -- to exclude dealerships in selection
list/nodefaults/nolinks/pagelength=0/noblanklines/noduplicates/domain="tour"
if itemnumber=1 then '"'+tour:code+'"' else
   ',"'+tour:code+'"'

sorted by
   tour:code
top of report
'string ctour[8192] = multivalue parameter prompt "Tour Operators: "'
"Valid "
end of report
'Default "ALL"'
;
run/parameterfile
rrepl "param_tour"

/*------------------------------------------------------------------------
 Create user interface include file
------------------------------------------------------------------------*/
define
  date dtsdate = parameter prompt "Starting Date" default todaysdate-day(todaysdate)+1
  date dtedate = parameter Prompt "Ending Date"  error "Error: End date < Start date" if dtEdate < dtSdate default todaysdate;
  include "param_loc.cq"
  include "param_locgroup.cq"
  include "param_product.cq"
  include "param_tour.cq"
  string ctype[1024] = multivalue parameter prompt "Tour Contracts to Include or 'ALL': " valid "Open Contracts","Closed Contracts" default "ALL"

;
replace tourrev_i1.inc
/*------------------------------------------------------------------------
 Retrieve applicable Tour contracts - setup parameters
------------------------------------------------------------------------*/
include "tourrev_i1.inc"

define

  file alt_locgroup1 = access locgroup,
    set locgroup:groupname = clocgroup,
    locgroup:locationcode = rrm:locationcodeout,
    null fill on failure

  file alt_locgroup2 = access locgroup,
    set locgroup:groupname = clocgroup,
    locgroup:locationcode = rrm:rptloccode,
    null fill on failure


where (((rrm:dateout >= dtSdate and rrm:dateout <= dtEdate) and (rrm:rastatus=2 and (ctype = "Open Contracts" or ctype = "ALL"))) or
       ((rrm:rptdate >= dtSdate and rrm:rptdate <= dtEdate) and (rrm:rastatus=3 and (ctype = "Closed Contracts" or ctype = "ALL"))) and 
       rrm:product = cproduct and 
       ((rrm:locationcodeout = cLoc and ctype = "Open Contract") or (rrm:rptloccode = cloc and ctype = "Closed Contracts") or cloc = "ALL") and 
       (cLocGroup = "" or (cLocGroup = alt_locgroup1:groupname and ctype = "Open Contracts") or (cLocGroup = alt_locgroup2:groupname and ctype = "Closed Contracts")))
 

list/domain="rrm"/hold="tourrev1_hf"
if (((rrm:tour = 1 and ctour = "ALL")  or rrm:tourcode = ctour) and rrm:tourcode <> "") then {
rrm:commonid/keyelement=1.1
rrm:tour
rrm:tourcode
rrm:ranumber
rrm:rastatus
rrm:dateout
rrm:timeout
rrm:datedue
rrm:timedue
rrm:datein
rrm:timein
rrm:rptdate
rrm:vouchernum
rrm:serviceordernum
rrm:tourdays
rrm:customerfirstname
rrm:customerlastname
}
;
replace tourrev_1.eq
run/setparameter tourrev_1.eq
run/parameterfile=tourrev_1 tourrev_1.eq

/*------------------------------------------------------------------------
 Collect revenue for tour contracts customer/tour
------------------------------------------------------------------------*/

define

  file system_set = access systemsettings,
   set systemsettings:dbid=""

  file rate_link = access ratelinera,
    set ratelinera:commonid=tourrev1_hf:commonid,
    with ratelinera:res = 0,
    one to many, zero fill on failure

  file rcmift_link = access rcmift,
    set rcmift:commonid=tourrev1_hf:commonid,
    with rcmift:rev=1,
    one to many, zero fill on failure

  number extra_day = if (tourrev1_hf:rastatus=2 and (tourrev1_hf:timedue - tourrev1_hf:timeout) > system_set:extraactday) then 1 else
                        if ((tourrev1_hf:timein - tourrev1_hf:timeout) > system_set:extraactday) then 1 else 0

  number tot_actdays = if tourrev1_hf:rastatus=2 then 
    if (tourrev1_hf:datedue - tourrev1_hf:dateout) = 0 then 1 + extra_day else (tourrev1_hf:datedue - tourrev1_hf:dateout)+ extra_day else
      if (tourrev1_hf:datein - tourrev1_hf:dateout) = 0 then 1 + extra_day else (tourrev1_hf:datein - tourrev1_hf:dateout)+ extra_day


  number rate_tour = if rate_link:tour = 1 then rate_link:totcharge else 0
  number rate_cust = if rate_link:tour = 0 then rate_link:totcharge else 0
  number misc_tour = if rcmift_link:tour = 1 then rcmift_link:total else 0
  number misc_cust = if rcmift_link:tour = 0 then rcmift_link:total else 0


list/domain="tourrev1_hf"/nodetail/hold="tourrev2_hf"
  tourrev1_hf:commonid 
  tourrev1_hf:ranumber
  rate_tour
  rate_cust
  misc_tour
  misc_cust
  tot_actdays

sorted by
  tourrev1_hf:commonid

end of tourrev1_hf:commonid
  tourrev1_hf:commonid /keyelement=1.1
  tourrev1_hf:ranumber
  total[rate_tour,tourrev1_hf:commonid]+total[misc_tour,tourrev1_hf:commonid]/name="tour_chgs"
  total[rate_cust,tourrev1_hf:commonid]+total[misc_cust,tourrev1_hf:commonid]/name="cust_chgs"
  tot_actdays/name="act_days"

;
run/parameterfile=tourrev_1

/*------------------------------------------------------------------------
 Output Report
------------------------------------------------------------------------*/
include "tourrev_i1.inc"

define

   file chrgs_link = access tourrev2_hf,
    set tourrev2_hf:commonid=tourrev1_hf:commonid,
    one to one, zero fill on failure   

list/domain="tourrev1_hf"/nobanner/pageof/title="Tour Revenue Report"/noheadings/pagelength=60/count
  if tourrev1_hf:rastatus=2 then tourrev1_hf:ranumber+" *" else tourrev1_hf:ranumber/displaywidth=10/name="ranum"
  if tourrev1_hf:rastatus=2 then tourrev1_hf:datedue else tourrev1_hf:rptdate/displaywidth=12/name="datein"
  tourrev1_hf:customerlastname/displaywidth=20
  tourrev1_hf:customerfirstname/displaywidth=10
  chrgs_link:tour_chgs/displaywidth=12/total
  chrgs_link:cust_chgs/displaywidth=12/total
  chrgs_link:tour_chgs+chrgs_link:cust_chgs/displaywidth=12/total/name="totchrgs"
  tourrev1_hf:dateout/displaywidth=12
  tourrev1_hf:tourdays/displaywidth=10/total
  chrgs_link:act_days/displaywidth=10/total

sorted by
  tourrev1_hf:tourcode/total/count/heading="@"
  tourrev1_hf:ranumber
  
top of page

 "company_logo.jpg"/image/width=20/left/newline

 box/center/column=1

  "Date Range: "+str(dtsdate)+" to "+str(dtedate)
  "Product: " + str(cProduct)+"   "/newline
  if cLocgroup <> "" then "Location group: " + str(cLocGroup) else "Locations: " + str(cLoc)/displaywidth=100/newline
  if ctype = "ALL" then "All Contracts" else str(ctype)/newline
  "Tour: " + str(ctour)/newline
  end box /newline=4

  "RA#"/align=ranum
  "Due/Rpt"/align=datein
  "Last Name"/align=tourrev1_hf:customerlastname
  "First Name"/align=tourrev1_hf:customerfirstname
  "Tour Chgs"/align=chrgs_link:tour_chgs
  "Renter Chgs"/align=chrgs_link:cust_chgs
  "Est/Charges"/align=totchrgs
  "Date Out"/align=tourrev1_hf:dateout
  "Tour Days"/align=tourrev1_hf:tourdays
  "# Days"/align=chrgs_link:act_days/newline

top of tourrev1_hf:tourcode
  "Tour Operator: "+str(tourrev1_hf:tourcode)/newline
 
end of report
"  *  indicates open contracts"

;
replace tourrev.eq
run/parameterfile=tourrev_1 tourrev.eq
rrepl tourrev

/*------------------------------------------------------------------------
 Send report to screen
------------------------------------------------------------------------*/
view tourrev

/*------------------------------------------------------------------------
 Prepare for job submission to RW
------------------------------------------------------------------------*/

define
   string action      = "Report" -- Post or Report
   string rptdesc     = "Tour Revenue Report" -- Report Title
   string rptname1    = "tourrev" -- output file1
   string rptname2    = "" -- output file2 if applicable
   string rptname3    = "" -- output file3 if applicable
   string parfile     = "tourrev_1.par" -- parameter.par file
   string xmlname     = "" -- xml for posting if applicable
   string exportname  = "" -- posting export file if applicable
   string startdate   = ""  -- requires: include "parameter.inc" for this value if needed
   string enddate     = ""  -- requires: include "parameter.inc" for this value if needed


list/domain="dummy"/nodefaults/nobanner
action/newline
rptdesc/newline
rptname1/newline
rptname2/newline
rptname3/newline
parfile/newline
xmlname/newline
exportname/newline
startdate/newline
enddate/newline
;
run/parameterfile
rrepl rwsubmitpar
run/parameterfile rwsubmitjob.mf
