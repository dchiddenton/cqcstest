clear echo

description

  Copyright by Bluebird Auto Rental Systems Ltd 2019.
  All rights reserved.

  Report: Claims Detail Report
  Author: David Chiddenton
  $Revision: 2 $;
  
/*------------------------------------------------------------------------
Created: 4/4/2018
Modified:

Notes:
$Workfile: claimsdetail.mf $                              
$Archive: /AutoMate2/Version 5.0/Source/cqcs/claimsdetail.mf $
$Revision: 2 $
$History: claimsdetail.mf $
 * 
 * *****************  Version 2  *****************
 * User: Dchidden     Date: 9/12/19    Time: 11:52a
 * Updated in $/AutoMate2/Version 5.0/Source/cqcs
 * Add revision number to view in About screen
 * 
 * *****************  Version 1  *****************
 * User: Dchidden     Date: 9/11/19    Time: 1:27p
 * Created in $/AutoMate2/Version 5.0/Source/cqcs
 * Initial creation
------------------------------------------------------------------------*/

/*------------------------------------------------------------------------
 Retrieve standard parameters and create custom parameters
------------------------------------------------------------------------*/

run/parameterfile parameters.mf

viewpoint native;
define
   number num_params = countfields(claimsetup:explist,",")  

   file i_counter = access counter,
    set min=1,max=num_params

list/nodefaults/nolinks/pagelength=0/noblanklines/noduplicates/domain="claimsetup"
if i_counter:position <= num_params then
  if i_counter:position=1 then '"'+subfield(claimsetup:explist,",",i_counter:position)+'"' else
   ',"'+subfield(claimsetup:explist,",",i_counter:position)+'"'

top of report
'string cexp[8192] = multivalue parameter prompt "Expense Types: "'
"Valid "
end of report
'Default "ALL"'
;
run/parameterfile
rrepl "param_exp"

list/nodefaults/nolinks/pagelength=0/noblanklines/noduplicates/domain="paymentstype"
if itemnumber=1 then '"'+paymentstype:paymenttype+'"' else
   ',"'+paymentstype:paymenttype+'"'

sorted by
   paymentstype:paymenttype
top of report
'string cpmt[8192] = multivalue parameter prompt "Payment Types: "'
"Valid "
end of report
'Default "ALL"'
;
run/parameterfile
rrepl "param_paytype"

viewpoint native;
define
   number num_params = countfields(claimsetup:recoverysource,",")  

   file i_counter = access counter,
    set min=1,max=num_params

list/nodefaults/nolinks/pagelength=0/noblanklines/noduplicates/domain="claimsetup"
if i_counter:position <= num_params then
  if i_counter:position=1 then '"'+subfield(claimsetup:recoverysource,",",i_counter:position)+'"' else
   ',"'+subfield(claimsetup:recoverysource,",",i_counter:position)+'"'

top of report
'string crecovery[8192] = multivalue parameter prompt "Recovery Codes: "'
"Valid "
end of report
'Default "ALL"'
;
run/parameterfile
rrepl "param_recovery"

viewpoint native;
define
   number num_params = countfields(claimsetup:typelist,",")  

   file i_counter = access counter,
    set min=1,max=num_params

list/nodefaults/nolinks/pagelength=0/noblanklines/noduplicates/domain="claimsetup"
if i_counter:position <= num_params then
   if i_counter:position=1 then '"'+subfield(claimsetup:typelist,",",i_counter:position)+'"' else
   ',"'+subfield(claimsetup:typelist,",",i_counter:position)+'"'

top of report
'string ctype[8192] = multivalue parameter prompt "Claim Types: "'
"Valid "
end of report
'Default "ALL"'
;
run/parameterfile
rrepl "param_ctype"

viewpoint native;
define
   number num_params = countfields(claimsetup:statlist,",")  

   file i_counter = access counter,
    set min=1,max=num_params

list/nodefaults/nolinks/pagelength=0/noblanklines/noduplicates/domain="claimsetup"
if i_counter:position <= num_params then
   if i_counter:position=1 then '"'+subfield(claimsetup:statlist,",",i_counter:position)+'"' else
   ',"'+subfield(claimsetup:statlist,",",i_counter:position)+'"'

top of report
'string ccstatus[8192] = multivalue parameter prompt "Claim Statuses: "'
"Valid "
end of report
'Default "ALL"'
;
run/parameterfile
rrepl "param_cstatus"

list/nodefaults/nolinks/pagelength=0/noblanklines/noduplicates/domain="employee"
if itemnumber=1 then '"'+employee:employeelastname+', '+employee:employeefirstname+'"' else
   ',"'+employee:employeelastname+', '+employee:employeefirstname+'"'

sorted by
   employee:employeelastname
top of report
'string cemps[8192] = multivalue parameter prompt "Employees to Include: "'
"Valid "
end of report
'Default "ALL"'
;
run/parameterfile
rrepl "param_emps"

/*------------------------------------------------------------------------
 Create user interface include file
------------------------------------------------------------------------*/
define
  date dtsdate = parameter prompt "Starting Date: " default (todaysdate-1) - day(todaysdate-1) + 1
  date dtedate = parameter prompt "Ending Date: " error "Error: End date < Start date" if dtedate < dtsdate default todaysdate - 1
  include "param_loc.cq"
  include "param_locgroup.cq"
  include "param_product.cq"
  string cunits = range parameter prompt "Enter Unit Range (Start..End or ALL): " default ALL
  include "param_emps.cq"
  include "param_ctype.cq"
  include "param_cstatus.cq"
  include "param_recovery.cq"
  include "param_exp.cq"
  include "param_paytype.cq"
  string cpositive = parameter/uppercase prompt "Print Positive Balance Claims: " valid "Y","N" default "Y"
  string czero = parameter/uppercase prompt "Print Zero Balance Claims: " valid "Y","N" default "Y"
  string cclosed = parameter/uppercase prompt "Print Closed Claims: " valid "Y","N" default "N"
  string cnotes = parameter valid "None","Summary","Detail" prompt "Print Note Detail?" default "None"
  string ccalculation = parameter valid "Use Expenses","Use Subrogation" prompt "Net Calculation?" default "Use Expenses"
  string cselectdate = parameter prompt "Select by: " valid "Date of Loss","Close Date" default "Date of Loss"
  string cexppmt = parameter/uppercase prompt "Print RA's with No Expenses or Payments: " valid "Y","N" default "N"

;
replace claimsdetail_i1.inc
/*------------------------------------------------------------------------
 Retrieve claim records - setup parameters
------------------------------------------------------------------------*/
viewpoint native;
include "claimsdetail_i1.inc"
define
   number itemp = (todaysdate - claim:dateentered)
   string claim_age = if itemp < 30 then "30" else
                      if itemp >= 30 and itemp < 60 then "60" else
                      if itemp >= 60 and itemp < 90 then "90" else
                      if itemp >= 90 then "Over 90"

   file alt_locgroup = access locgroup,
    set locgroup:groupname = clocgroup,
    locgroup:locationcode = claim:rentingloc,
    null fill on failure

   file contact_link = access claimcontact,
    set claimcontact:commonid=claim:commonid,
    with claimcontact:type = "Renter",
    one to one, generic, null fill on failure

   file rrm_link = access rrm,
    set rrm:ranumber=claim:ranumber,
    one to one, null fill on failure

   file rental_link = access rentaldb,
    set rentaldb:commonid=rrm_link:commonid,
    one to one, null fill on failure

  file vehicle_link = access claimvehicle,
   set claimvehicle:commonid=claim:commonid,
   one to one, generic, null fill on failure

where (claim:type = ctype or ctype = "ALL") and
      (claim:stat = ccstatus or ccstatus = "ALL") and
      (claim:recoverysource = crecovery or crecovery = "ALL") and 
      (claim:rentingloc = cloc or cloc = "ALL") and
      (cLocGroup = "" or cLocGroup = alt_locgroup:groupname) and
      (claim:workingemp = cemps or cemps = "ALL") and
      ((((claim:dol >= dtsdate and claim:dol <= dtedate) or claim:dol = null) and cselectdate = "Date of Loss")or
       (((claim:closedate >= dtsdate and claim:closedate <= dtedate)) and cselectdate = "Close Date")) and
      (vehicle_link:unitnumber = cunits or cunits = "ALL") and
      (vehicle_link:product = cproduct or cproduct = "ALL")
  

list/domain="claim"/hold="claimdetail_hf"
  claim:commonid/keyelement=1.1
  claim:claimnumdol 
  claim:claimnumseq 
  claim:claimnumunit
  claim:ranumber
  claim:sourcecode
  claim:type
  claim:stat
  claim:recoverysource
  claim:dol
  claim:dateentered
  claim:closedate
  claim:datemodified
  claim:ldw
  claim:subroamt
  claim:estimateamt
  claim:workingemp
  claim:enteringemp
  claim_age
  contact_link:lastname/name="renter"
  rental_link:ratecode/name="rate_code"
  vehicle_link:unitnumber
  claim:rentingloc


;
replace claimsdetail_1.eq
run/setparameter claimsdetail_1.eq

run/parameterfile=claimsdetail_1 claimsdetail_1.eq

/*------------------------------------------------------------------------
 Gather claim expenses
------------------------------------------------------------------------*/

viewpoint native;
include "claimsdetail_i1.inc"

define
   file claim_exp = access claimexp,
    set claimexp:commonid=claimdetail_hf:commonid,
    one to many, zero fill on failure

   string exptype = if claim_exp:type = "" then "N/A" else claim_exp:type

list/domain="claimdetail_hf"/hold="claimexp_hf"
if (claim_exp:type = cexp or cexp = "ALL") and claim_exp:commonid <> 0 then {
  claimdetail_hf:commonid/keyelement=1.1
  claim_exp:datecharged
  claim_exp:invid
  exptype/name="type"
  claim_exp:roid
  claim_exp:paidto
  claim_exp:amount
}

;
run/parameterfile=claimsdetail_1

/*------------------------------------------------------------------------
 Summarize expenses
------------------------------------------------------------------------*/
sum/domain="claimexp_hf"/hold="claimexp2_hf"
  claimexp_hf:amount

across
  claimexp_hf:type

by
  claimexp_hf:commonid/keyelement=1.1

;
run/parameterfile=claimsdetail_1 

/*------------------------------------------------------------------------
 Create expense fields
------------------------------------------------------------------------*/
define
  
  string cd1 = replace(str(claimexp_hf:type)," ","_") -- remove special characters for field definition
  string cd2 = replace(str(cd1),"-","_")  
  string cd3 = replace(str(cd2),"$","_") 
  string cd4 = replace(str(cd3),"%","_")  
  string cd5 = replace(str(cd4),"/","_")  
  string cd6 = replace(str(cd5),"\","_")  
  string cd7 = replace(str(cd6),"!","_")  
  string cd8 = replace(str(cd7),"@","_")  
  string cd9 = replace(str(cd8),"#","_")  
  string cd10 = replace(str(cd9),"^","_")  
  string cd11 = replace(str(cd10),"=","_")  
  string cd12 = replace(str(cd11),"&","_")  
  string cd13 = replace(str(cd12),"`","_")  
  string cd14 = replace(str(cd13),"~","_") 
  string cd15 = replace(str(cd14),"*","_") 
  string cd16 = replace(str(cd15),"(","_") 
  string cd17 = replace(str(cd16),")","_") 
  string cd18 = replace(str(cd17),"+","_") 
  string cd19 = replace(str(cd18),"<","_") 
  string cd20 = replace(str(cd19),">","_") 
  string cd21 = replace(str(cd20),"?","_") 
  string cd22 = replace(str(cd21),"{","_") 
  string cd23 = replace(str(cd22),"}","_")
  string cd24 = replace(str(cd23),"[","_")
  string cd25 = replace(str(cd24),"]","_")
  string cd26 = replace(str(cd25),"|","_") 
  string cd27 = replace(str(cd26),":","_")
  string cd28 = replace(str(cd27),";","_")
  string cd29 = replace(str(cd28),".","_")
  
  string cd_final = replace(str(cd29),"*","_")

list/nodetail/nobanner/nodefaults/noreporttotals/domain="claimexp_hf"
  claimexp_hf:type

sorted by
  claimexp_hf:type

end of claimexp_hf:type
      'exp_link:'+'amount_'+cd_final+'/name='+'"col_'+cd_final+'"'

;
run/parameterfile=claimsdetail_1 
rrepl claim_detail


/*------------------------------------------------------------------------
 Create expense columns
------------------------------------------------------------------------*/
define
  
  string cd1 = replace(str(claimexp_hf:type)," ","_") -- remove special characters for field definition
  string cd2 = replace(str(cd1),"-","_")  
  string cd3 = replace(str(cd2),"$","_") 
  string cd4 = replace(str(cd3),"%","_")  
  string cd5 = replace(str(cd4),"/","_")  
  string cd6 = replace(str(cd5),"\","_")  
  string cd7 = replace(str(cd6),"!","_")  
  string cd8 = replace(str(cd7),"@","_")  
  string cd9 = replace(str(cd8),"#","_")  
  string cd10 = replace(str(cd9),"^","_")  
  string cd11 = replace(str(cd10),"=","_")  
  string cd12 = replace(str(cd11),"&","_")  
  string cd13 = replace(str(cd12),"`","_")  
  string cd14 = replace(str(cd13),"~","_") 
  string cd15 = replace(str(cd14),"*","_") 
  string cd16 = replace(str(cd15),"(","_") 
  string cd17 = replace(str(cd16),")","_") 
  string cd18 = replace(str(cd17),"+","_") 
  string cd19 = replace(str(cd18),"<","_") 
  string cd20 = replace(str(cd19),">","_") 
  string cd21 = replace(str(cd20),"?","_") 
  string cd22 = replace(str(cd21),"{","_") 
  string cd23 = replace(str(cd22),"}","_")
  string cd24 = replace(str(cd23),"[","_")
  string cd25 = replace(str(cd24),"]","_")
  string cd26 = replace(str(cd25),"|","_") 
  string cd27 = replace(str(cd26),":","_")
  string cd28 = replace(str(cd27),";","_")
  string cd29 = replace(str(cd28),".","_")
  
  string cd_final = replace(str(cd29),"*","_")

list/nodetail/nobanner/nodefaults/noreporttotals/domain="claimexp_hf"
  claimexp_hf:type

sorted by
  claimexp_hf:type

end of claimexp_hf:type
      '"'+claimexp_hf:type+'"'+'/align=col_'+cd_final

;
run/parameterfile=claimsdetail_1
rrepl claim_col

/*------------------------------------------------------------------------
 Gather claim payments
------------------------------------------------------------------------*/

viewpoint native;
include "claimsdetail_i1.inc"

define
   file claim_pmt = access claimpmt,
    set claimpmt:commonid=claimdetail_hf:commonid,
    one to many, zero fill on failure

list/domain="claimdetail_hf"/hold="claimpmt_hf"
if (claim_pmt:type = cpmt or cpmt = "ALL") and claim_pmt:commonid <> 0 then {
  claimdetail_hf:commonid/keyelement=1.1
  claim_pmt:daterec
  claim_pmt:type
  claim_pmt:recfrom
  claim_pmt:amount
}

;
run/parameterfile=claimsdetail_1

/*------------------------------------------------------------------------
 Total claim expenses/payments
------------------------------------------------------------------------*/
viewpoint native;

define
   file claim_pmt = access claimpmt_hf,
    set claimpmt_hf:commonid=claimdetail_hf:commonid,
    one to many, zero fill on failure

   file claim_exp = access claimexp_hf,
    set claimexp_hf:commonid=claimdetail_hf:commonid,
    one to many, zero fill on failure


list/domain="claimdetail_hf"/nodetail/hold="claimtot_hf"
  claimdetail_hf:commonid
  claim_exp:amount
  claim_pmt:amount

sorted by claimdetail_hf:commonid

end of claimdetail_hf:commonid
  claimdetail_hf:commonid/keyelement=1.1
  total[claim_exp:amount]/name="exptot"
  total[claim_pmt:amount]/name="pmttot"

;
run/parameterfile=claimsdetail_1

/*------------------------------------------------------------------------
 Consolidate exp/pmts
------------------------------------------------------------------------*/
viewpoint native;
define
   file exp_link = access claimexp_hf,
    set claimexp_hf:commonid=claimdetail_hf:commonid,
    one to many, zero fill on failure

  file pmt_link = access claimpmt_hf,
    set claimpmt_hf:commonid=claimpmt_hf:commonid,
    one to many, zero fill on failure

list/domain="claimdetail_hf"/hold="claimprt_hf"
if exp_link:amount <> 0 or pmt_link:amount <> 0 then {
  claimdetail_hf:commonid/keyelement=1.1
  exp_link:amount
  pmt_link:amount
}

;
run/parameterfile=claimsdetail_1

/*------------------------------------------------------------------------
 Output Report
------------------------------------------------------------------------*/
viewpoint native;

include "claimsdetail_i1.inc" 

define

  file claimstot_link = access claimtot_hf,
   set claimtot_hf:commonid=claimdetail_hf:commonid,
   one to one, zero fill on failure

   file exp_link = access claimexp2_hf,
    set claimexp2_hf:commonid=claimdetail_hf:commonid,
    one to one, zero fill on failure
 
  string claim_num = str(claimdetail_hf:claimnumdol)+"-"+str(claimdetail_hf:claimnumunit)+"-"+str(claimdetail_hf:claimnumseq)

  number claim_net = claimstot_link:pmttot - claimstot_link:exptot


where (claim_net <> 0 or czero = "Y") and
      (claim_net < 0 or cpositive = "Y") and
      (claimdetail_hf:stat <> "Closed" or cclosed = "Y")

list/domain="claimdetail_hf"/nobanner/pageof/title="Claims Detail Report"/duplicates/noheadings  

claim_num/displaywidth=30
claimdetail_hf:renter/displaywidth=25
claimdetail_hf:stat/displaywidth=15
claimdetail_hf:sourcecode/displaywidth=15
claimdetail_hf:type/displaywidth=15
claimdetail_hf:recoverysource/displaywidth=15
claimdetail_hf:datemodified/displaywidth=10
claimdetail_hf:closedate/displaywidth=10
claimdetail_hf:ldw/displaywidth=5
claimdetail_hf:estimateamt/displaywidth=12

include "claim_detail.cq"

claimstot_link:exptot/total/displaywidth=12/name="aa"
claimstot_link:pmttot/total/displaywidth=12/name="bb"
claim_net/displaywidth=12
claimdetail_hf:subroamt/displaywidth=12
claimdetail_hf:unitnumber/displaywidth=10
claimdetail_hf:claimnumseq/displaywidth=8
claimdetail_hf:rate_code/displaywidth=10
claimdetail_hf:ranumber/displaywidth=10
claimdetail_hf:workingemp/displaywidth=10
claimdetail_hf:enteringemp/displaywidth=10
claimdetail_hf:rentingloc/displaywidth=10


sorted by claimdetail_hf:dol

top of page
 "company_logo.jpg"/image/width=20/left/newline

 box/center/column=1
  "Date Range: " + str(dtSDate) + " to " + str(dtEdate)/newline
  if cunits = "ALL" then {"Product(s): " + str(cproduct)+"  All Units"/newline} else {"Product(s): " + str(cproduct)+"  Unit Range: "+str(cunits)/displaywidth=100/newline}
  if cLocgroup <> "" then "Location group: " + str(cLocGroup) else "Locations: " + str(cLoc)/displaywidth=100/newline
  "Selected by "+lowercase(cselectdate)/newline
  "Type: "+str(ctype) + "  Status: "+str(ccstatus)/displaywidth=100/newline
  end box/newlines=4

"Claim #"/align=claim_num
"Renter"/align=claimdetail_hf:renter
"Status"/align=claimdetail_hf:stat
"Source"/align=claimdetail_hf:sourcecode
"Type"/align=claimdetail_hf:type
"Recovery"/align=claimdetail_hf:recoverysource
"Last Mod"/align=claimdetail_hf:datemodified
"Close Date"/align=claimdetail_hf:closedate
"LDW"/align=claimdetail_hf:ldw
"Estimate"/align=claimdetail_hf:estimateamt

include "claim_col.cq"

"Expenses"/align=aa
"Payments"/align=bb
"Net"/align=claim_net
"Subro"/align=claimdetail_hf:subroamt
"Unit#"/align=claimdetail_hf:unitnumber
"Claim Seq"/align=claimdetail_hf:claimnumseq
"Rate Code"/align=claimdetail_hf:rate_code
"RA Number"/align=claimdetail_hf:ranumber
"Managed by"/align=claimdetail_hf:workingemp
"Entered By"/align=claimdetail_hf:enteringemp
"Renting Location"/align=claimdetail_hf:rentingloc/newline


end of report
"Total Claims: "+str(count[claim_num,report])

;
replace claimsdetail.eq
run/parameterfile=claimsdetail_1 claimsdetail.eq
rrepl claimsdetail

/*------------------------------------------------------------------------
 Send report to screen
------------------------------------------------------------------------*/
view claimsdetail

/*------------------------------------------------------------------------
 Prepare for job submission to RW
------------------------------------------------------------------------*/

define
   string action      = "Report" -- Post or Report
   string rptdesc     = "Claims Detail Report" -- Report Title
   string rptname1    = "claimsdetail" -- output file1
   string rptname2    = "" -- output file2 if applicable
   string rptname3    = "" -- output file3 if applicable
   string parfile     = "claimsdetail_1.par" -- parameter.par file
   string xmlname     = "" -- xml for posting if applicable
   string exportname  = "" -- posting export file if applicable
   string startdate   = ""  -- requires: include "parameter.inc" for this value if needed
   string enddate     = ""  -- requires: include "parameter.inc" for this value if needed


list/domain="dummy"/nodefaults/nobanner
action/newline
rptdesc/newline
rptname1/newline
rptname2/newline
rptname3/newline
parfile/newline
xmlname/newline
exportname/newline
startdate/newline
enddate/newline
;
run/parameterfile
rrepl rwsubmitpar
run/parameterfile rwsubmitjob.mf
