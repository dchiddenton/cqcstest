clear echo

description

  Copyright by Bluebird Auto Rental Systems Ltd 2019.
  All rights reserved.

  Report: Booked Reservation Report
  Author: David Chiddenton
  $Revision: 2 $;
  
/*------------------------------------------------------------------------
Created: 12/1/2017
Modified:

Notes:
$Workfile: bookrez.mf $                              
$Archive: /AutoMate2/Version 5.0/Source/cqcs/bookrez.mf $
$Revision: 2 $
$History: bookrez.mf $
 * 
 * *****************  Version 2  *****************
 * User: Dchidden     Date: 9/12/19    Time: 11:52a
 * Updated in $/AutoMate2/Version 5.0/Source/cqcs
 * Add revision number to view in About screen
 * 
 * *****************  Version 1  *****************
 * User: Dchidden     Date: 9/11/19    Time: 1:27p
 * Created in $/AutoMate2/Version 5.0/Source/cqcs
 * Initial creation
------------------------------------------------------------------------*/

/*------------------------------------------------------------------------
 Retrieve standard parameters and create 
------------------------------------------------------------------------*/

run/parameterfile parameters.mf

list/nodefaults/nolinks/pagelength=0/noblanklines/noduplicates/domain="sources"
if itemnumber=1 then '"'+sources:sourcecode+'"' else
   ',"'+sources:sourcecode+'"'

sorted by
   sources:sourcecode
top of report
'string csource[8192] = multivalue parameter prompt "Sources Codes: "'
"Valid "
end of report
'Default "ALL"'
;
run/parameterfile
rrepl "param_sources"

/*------------------------------------------------------------------------
 Create user interface include file
------------------------------------------------------------------------*/
define
  date dtsdate = parameter prompt "Start Date: " default todaysdate
  date dtedate = parameter prompt "End Date: " error "Error: End date < Start date" if dtedate < dtsdate default todaysdate + 5
  include "param_loc.cq"
  include "param_locgroup.cq"
  include "param_product.cq"
  include "param_sources.cq"
  string cdate = parameter prompt "Select by:" valid "Date Added","Date Out" Default "Date Added"
  string ctaxes = parameter/uppercase prompt "Include Taxes in Renvenue Total? " valid "Y","N" default "N"
  string cdetail[ 1024 ] = multivalue parameter prompt "Select Additonal Info: " valid "None","Rate Detail","Revenue Detail","Misc Info" default "None"
;
replace bookrez_i1.inc

/*------------------------------------------------------------------------
 Dynamic record selection - setup parameters
------------------------------------------------------------------------*/
viewpoint native;

  include "bookrez_i1.inc"
define

  string date_line = if cdate = "Date Out" then "and (rrm:dateout >= dtSDate and rrm:dateout <= dtEdate )" else
   "and (resdb_link:dateadded >= dtSDate and resdb_link:dateadded <= dtEdate )"
 


list/nodefaults/noheadings/nobanner/domain="dummy"

"where"/newline
  "(rrm:resstatus <> 0)"/newline
  date_line/newline
  "and (rrm:Product  = cProduct or cProduct = 'ALL' )"/newline
  "and (rrm:locationcodeout = cLoc or cLoc = 'ALL' )"/newline
  'and (cLocGroup = "" or cLocGroup = alt_locgroup:groupname)'/newline
  "and (rrm:sourcecode = csource or csource = 'ALL')"/newline

;
replace bookrez_1.eq
run/setparameter bookrez_1.eq

run/parameterfile=bookrez_1 bookrez_1.eq
rrepl bookrez_i1

/*------------------------------------------------------------------------
 Retrieve RRM records
------------------------------------------------------------------------*/

  include "bookrez_i1.inc"

define


  file alt_locgroup = access locgroup,
    set locgroup:groupname = clocgroup,
    locgroup:locationcode = rrm:locationcodeout,
    null fill on failure

  file resdb_link = access resdb,
   set resdb:commonid=rrm:commonid,
   one to one, null fill on failure

  file rental_link = access rentaldb,
   set rentaldb:commonid=rrm:commonid,
   one to one, zero fill on failure

  file inv_link = access invtrx,
   set invtrx:invtrxid=rrm:invtrxid,
   one to one,generic,null fill on failure

  file source_link = access sourcereferrals,
   set sourcereferrals:sourcecode=rrm:sourcecode,
   sourcereferrals:referralcode=rrm:referralcode,
   one to one,null fill on failure
  

  define string cHCResStatus = switch (RRM:ResStatus) 
    case 0 : "" -- not a rez
    case 10 : "Quote" 
    case 15 : "Pending Res"  
    case 20 : "Open Reservation"  
    case 30 : "Open Contract"  
    case 40 : "Closed Contract"  
    case 50 : "Cancelled"  
    case 60 : "No-Show"  
    case 70 : "Void"  
    case 80 : "Void Reservation"  
    default : "Invalid Res Status"

  number timerev = if rental_link:commonid <> 0 then rental_link:totaltime else resdb_link:totaltime
  number mlgrev = if rental_link:commonid <> 0 then rental_link:totalmiles else 0
  number tmdisc = if rental_link:commonid <> 0 then rental_link:rentaldiscountamt else resdb_link:rentaldiscountamt
  number nettm = if rental_link:commonid <> 0 then rental_link:totaltpm - rental_link:rentaldiscountamt else resdb_link:totaltpm - resdb_link:rentaldiscountamt
  
 include "bookrez_i1.cq"


list/domain="rrm"/hold="bookrez_hf"
   resdb_link:CommonID/keyelement=1.1
   rrm:LocationCodeOut
   resdb_link:ResNumber
   resdb_link:DateAdded
   rrm:NoShowDate
   rrm:RANumber
   rrm:Company
   rrm:CustomerLastName + "," + rrm:CustomerFirstName/name="renter"
   rrm:ReservedClass
   rrm:RentedAsClass
   resdb_link:DateOut
   resdb_link:TimeOut
   rrm:DateIn
   rrm:TimeIn
   rrm:SourceCode
   rrm:ReferralCode
   source_link:name/name="referralname"
   resdb_link:RateCode
   resdb_link:RatePlan
   timerev
   mlgrev
   tmdisc
   nettm
   resdb_link:AddedByEmployeeNumber
   resdb_link:ChangedByEmployeeNumber
   cHCResStatus/name="resstat"
   inv_link:unitnumber

;
run/parameterfile=bookrez_1 

/*------------------------------------------------------------------------
 Create no record dummy record
------------------------------------------------------------------------*/
viewpoint native;
define 
   file counter_i = access counter,
    set min=1,max=12

list/domain="dummy"/hold="rates_hf"
if counter_i:position < 13 then {
  0/width=19/name="commonid"/keyelement=1.1
  counter_i:position/width=19/name="itemp"
  0/width=19/name="rec_num"
  0/width=19/decimalplaces=2/name="rateper"
}
followed by
  0
  13
  1
  0
followed by
  0
  13
  2
  0
followed by
  0
  13
  3
  0
followed by
  0
  13
  4
  0
followed by
  0
  13
  5
  0
followed by
  0
  13
  6
  0

;
run/parameterfile=bookrez_1 


/*------------------------------------------------------------------------
 Calculate rate structure
------------------------------------------------------------------------*/
viewpoint native;

define
  file rateline_link = access ratelinera,
    set ratelinera:commonid = bookrez_hf:commonid,
    one to many
  
  number itemp1 = switch(rateline_link:minsperx)
   case 60                      : 1   --hourly
   case 1440                    : 3   --daily
   case 10080                   : 5   --weekly
   case 40320,41760,43200,44640 : 6  --monthly
   default                      : 13 --tiered
  
  number ot_flag = if itemp1 < 4 and rateline_link:type <> "Reg" then 1 else 0
  
  file rate_link = access ratera,
    set ratera:commonid = rateline_link:commonid,
    ratera:rateid = rateline_link:rateid,
    one to one
  
  number spec_flag = if rate_link:assdrtreq = 1 or (rate_link:assortid <> 0 and str(rate_link:assortid) <> null) then 6 else 0
  
  number itemp = if rate_link:calcmeth = 3 or rateline_link:maxunits <> 0 then 13 else itemp1 + ot_flag + spec_flag
  
  file rental_link = access rentaldb,
    set rentaldb:commonid = bookrez_hf:commonid,
    one to one,
    zero fill on failure

list/domain="bookrez_hf"/hold="rates_hf"/append
  if (rental_link:commonid > 0 and rateline_link:res = 0) or (rental_link:commonid = 0 and rateline_link:res = 1) then {
  bookrez_hf:commonid/width=19/keyelement=1.1
  itemp/width=19
  if itemp=13 then running count[itemp,bookrez_hf:commonid] else 0/width=19/name="rec_num"
  rateline_link:rateper/width=19/decimalplaces=2
  }
sorted by
  bookrez_hf:commonid
  itemp/descending

;
run/parameterfile=bookrez_1 
/*------------------------------------------------------------------------
 Create rate buckets
------------------------------------------------------------------------*/

where 
  rates_hf:itemp = 
    13
sum/domain="rates_hf"/nototals/hold="tiers_hf"
  rates_hf:rateper
across
  rates_hf:rec_num/showall
by
  rates_hf:commonid/keyelement=1.1
;
run/parameterfile=bookrez_1 

/*------------------------------------------------------------------------
 Create tier buckets
------------------------------------------------------------------------*/
where 
  rates_hf:itemp<>
   13
sum/domain="rates_hf"/nototals/hold="rateline_hf"
  rates_hf:rateper
across
  rates_hf:itemp/showall
by
  rates_hf:commonid/keyelement=1.1
;
run/parameterfile=bookrez_1 

/*------------------------------------------------------------------------
 Retrieve misc income
------------------------------------------------------------------------*/

include "bookrez_i1.inc"
define
   
  file rcmift_link = access rcmift,
   set rcmift:commonid=bookrez_hf:commonid,
   with rcmift:rev=1,
   one to many, null fill on failure


list/domain="bookrez_hf"/hold="rev_hf"
if ((rcmift_link:type < 50 and ctaxes = "N") or ctaxes="Y") and (rcmift_link:total <> 0) then {
    bookrez_hf:commonid
    rcmift_link:total
    rcmift_link:code
}
;
run/parameterfile=bookrez_1 

/*------------------------------------------------------------------------
 Summarize misc income
------------------------------------------------------------------------*/

define
  
  string cd1 = replace(str(rev_hf:code)," ","_") -- remove special characters for field definition
  string cd2 = replace(str(cd1),"-","_")  
  string cd3 = replace(str(cd2),"$","_") 
  string cd4 = replace(str(cd3),"%","_")  
  string cd5 = replace(str(cd4),"/","_")  
  string cd6 = replace(str(cd5),"\","_")  
  string cd7 = replace(str(cd6),"!","_")  
  string cd8 = replace(str(cd7),"@","_")  
  string cd9 = replace(str(cd8),"#","_")  
  string cd10 = replace(str(cd9),"^","_")  
  string cd11 = replace(str(cd10),"=","_")  
  string cd12 = replace(str(cd11),"&","_")  
  string cd13 = replace(str(cd12),"`","_")  
  string cd14 = replace(str(cd13),"~","_") 
  string cd15 = replace(str(cd14),"*","_") 
  string cd16 = replace(str(cd15),"(","_") 
  string cd17 = replace(str(cd16),")","_") 
  string cd18 = replace(str(cd17),"+","_") 
  string cd19 = replace(str(cd18),"<","_") 
  string cd20 = replace(str(cd19),">","_") 
  string cd21 = replace(str(cd20),"?","_") 
  string cd22 = replace(str(cd21),"{","_") 
  string cd23 = replace(str(cd22),"}","_")
  string cd24 = replace(str(cd23),"[","_")
  string cd25 = replace(str(cd24),"]","_")
  string cd26 = replace(str(cd25),"|","_") 
  string cd27 = replace(str(cd26),":","_")
  string cd28 = replace(str(cd27),";","_")
  string cd_final = replace(str(cd28),"*","_")

where rev_hf:total <> 0

sum/domain="rev_hf"/hold="rev_sum_hf"
  rev_hf:total
across
  cd_final/total/singletotals
by
  rev_hf:commonid/keyelement=1.1
;
run/parameterfile=bookrez_1 

/*------------------------------------------------------------------------
 Create revenue lines
------------------------------------------------------------------------*/
define
  
  string cd1 = replace(str(rev_hf:code)," ","_") -- remove special characters for field definition
  string cd2 = replace(str(cd1),"-","_")  
  string cd3 = replace(str(cd2),"$","_") 
  string cd4 = replace(str(cd3),"%","_")  
  string cd5 = replace(str(cd4),"/","_")  
  string cd6 = replace(str(cd5),"\","_")  
  string cd7 = replace(str(cd6),"!","_")  
  string cd8 = replace(str(cd7),"@","_")  
  string cd9 = replace(str(cd8),"#","_")  
  string cd10 = replace(str(cd9),"^","_")  
  string cd11 = replace(str(cd10),"=","_")  
  string cd12 = replace(str(cd11),"&","_")  
  string cd13 = replace(str(cd12),"`","_")  
  string cd14 = replace(str(cd13),"~","_") 
  string cd15 = replace(str(cd14),"*","_") 
  string cd16 = replace(str(cd15),"(","_") 
  string cd17 = replace(str(cd16),")","_") 
  string cd18 = replace(str(cd17),"+","_") 
  string cd19 = replace(str(cd18),"<","_") 
  string cd20 = replace(str(cd19),">","_") 
  string cd21 = replace(str(cd20),"?","_") 
  string cd22 = replace(str(cd21),"{","_") 
  string cd23 = replace(str(cd22),"}","_")
  string cd24 = replace(str(cd23),"[","_")
  string cd25 = replace(str(cd24),"]","_")
  string cd26 = replace(str(cd25),"|","_") 
  string cd27 = replace(str(cd26),":","_")
  string cd28 = replace(str(cd27),";","_")
  string cd_final = replace(str(cd28),"*","_")

list/nodetail/nobanner/nodefaults/noreporttotals/domain="rev_hf"
  rev_hf:code

sorted by
  rev_hf:code

end of rev_hf:code 
  'rev_link:'+'total_'+cd_final+'/name='+'"col_'+cd_final+'"'
;
run/parameterfile=bookrez_1 
rreplace rev_lines

/*------------------------------------------------------------------------
 Create revenue headers
------------------------------------------------------------------------*/

define
  
  string cd1 = replace(str(rev_hf:code)," ","_") -- remove special characters for field definition
  string cd2 = replace(str(cd1),"-","_")  
  string cd3 = replace(str(cd2),"$","_") 
  string cd4 = replace(str(cd3),"%","_")  
  string cd5 = replace(str(cd4),"/","_")  
  string cd6 = replace(str(cd5),"\","_")  
  string cd7 = replace(str(cd6),"!","_")  
  string cd8 = replace(str(cd7),"@","_")  
  string cd9 = replace(str(cd8),"#","_")  
  string cd10 = replace(str(cd9),"^","_")  
  string cd11 = replace(str(cd10),"=","_")  
  string cd12 = replace(str(cd11),"&","_")  
  string cd13 = replace(str(cd12),"`","_")  
  string cd14 = replace(str(cd13),"~","_") 
  string cd15 = replace(str(cd14),"*","_") 
  string cd16 = replace(str(cd15),"(","_") 
  string cd17 = replace(str(cd16),")","_") 
  string cd18 = replace(str(cd17),"+","_") 
  string cd19 = replace(str(cd18),"<","_") 
  string cd20 = replace(str(cd19),">","_") 
  string cd21 = replace(str(cd20),"?","_") 
  string cd22 = replace(str(cd21),"{","_") 
  string cd23 = replace(str(cd22),"}","_")
  string cd24 = replace(str(cd23),"[","_")
  string cd25 = replace(str(cd24),"]","_")
  string cd26 = replace(str(cd25),"|","_") 
  string cd27 = replace(str(cd26),":","_")
  string cd28 = replace(str(cd27),";","_")
  string cd_final = replace(str(cd28),"*","_")

list/nodetail/nobanner/nodefaults/noreporttotals/domain="rev_hf"
  rev_hf:code

sorted by
  rev_hf:code

end of rev_hf:code
      '"'+rev_hf:code+'"'+'/align=col_'+cd_final
;
run/parameterfile=bookrez_1 
rreplace rev_headers

/*------------------------------------------------------------------------
 Create detail lines
------------------------------------------------------------------------*/
include "bookrez_i1.inc"


list/domain="dummy"/noreporttotals/nodefaults/nobanner
if cdetail = "Misc Info" then {
  'bookrez_hf:noshowdate/displaywidth=10/name="p"'/newline
  'bookrez_hf:ranumber/displaywidth=10/name="q"'/newline
  'bookrez_hf:company/displaywidth=20/name="r"'/newline
  'bookrez_hf:unitnumber/displaywidth=10/name="s"'/newline
  'bookrez_hf:datein/displaywidth=10/name="t"'/newline
  'bookrez_hf:timein/nototal/displaywidth=8/name="u"'/newline
  'num_days/displaywidth=5/name="v"'/newline
}

if cdetail = "Revenue Detail" then {
  'bookrez_hf:timerev/displaywidth=12/name="w"'/newline
  'bookrez_hf:mlgrev/displaywidth=12/name="x"'/newline
  'bookrez_hf:tmdisc/displaywidth=12/name="y"'/newline
  'bookrez_hf:nettm/displaywidth=12/name="z"'/newline
  'include "rev_lines.cq"'/newline
}
if cdetail = "Rate Detail" then {
  'rate_link:rateper_1/nototal/displaywidth=8/name="ra"'/newline
  'rate_link:rateper_2/nototal/displaywidth=8/name="rb"'/newline
  'rate_link:rateper_3/nototal/displaywidth=8/name="rc"'/newline
  'rate_link:rateper_4/nototal/displaywidth=8/name="rd"'/newline
  'rate_link:rateper_5/nototal/displaywidth=8/name="re"'/newline
  'rate_link:rateper_6/nototal/displaywidth=8/name="rf"'/newline
  'rate_link:rateper_7/nototal/displaywidth=8/name="rg"'/newline
  'rate_link:rateper_8/nototal/displaywidth=8/name="rh"'/newline
  'rate_link:rateper_9/nototal/displaywidth=8/name="ri"'/newline
  'rate_link:rateper_10/nototal/displaywidth=8/name="rj"'/newline
  'rate_link:rateper_11/nototal/displaywidth=8/name="rk"'/newline
  'rate_link:rateper_12/nototal/displaywidth=8/name="rl"'/newline
  'tier_link:rateper_1/nototal/displaywidth=8/name="rm"'/newline
  'tier_link:rateper_2/nototal/displaywidth=8/name="rn"'/newline
  'tier_link:rateper_3/nototal/displaywidth=8/name="ro"'/newline
  'tier_link:rateper_4/nototal/displaywidth=8/name="rp"'/newline
  'tier_link:rateper_5/nototal/displaywidth=8/name="rq"'/newline
  'tier_link:rateper_6/nototal/displaywidth=8/name="rr"'/newline
}

;
run/parameterfile=bookrez_1 
rreplace detail_lines

/*------------------------------------------------------------------------
 Create headers
------------------------------------------------------------------------*/
include "bookrez_i1.inc"


list/domain="dummy"/noreporttotals/nodefaults/nobanner
if cdetail = "Misc Info" then {
  '"No-Show Date" /align=p'/newline
  '"RA#"/align=q'/newline
  '"Company#"/align=r'/newline
  '"Unit#" /align=s'/newline
  '"Date In"/align=t'/newline
  '"Time In"/align=u'/newline
  '"Days"/align=v'/newline

}
if cdetail = "Revenue Detail" then {
  '"Time Rev"/align=w'/newline
  '"Mileage Rev"/align=x'/newline
  '"T&M Discount"/align=y'/newline
  '"Net T&M"/align=z'/newline 
  'include "rev_headers.cq"'/newline
}
if cdetail = "Rate Detail" then {
  '"Hourly"/align=ra'/newline
  '"OT Hour"/align=rb'/newline
  '"Daily"/align=rc'/newline
  '"EXDay"/align=rd'/newline
  '"Weekly"/align=re'/newline
  '"Monthly"/align=rf'/newline
  '"SPHourly"/align=rg'/newline
  '"SpOTHour"/align=rh '/newline
  '"SpDaily"/align=ri'/newline
  '"SpEXDay"/align=rj'/newline
  '"SpWeekly"/align=rk'/newline
  '"SpMonthly"/align=rl'/newline
  '"Tier1"/align=rm'/newline
  '"Tier2"/align=rn'/newline
  '"Tier3"/align=ro'/newline
  '"Tier4"/align=rp'/newline
  '"Tier5"/align=rq'/newline
  '"Last Tier"/align=rr'/newline
}
;
run/parameterfile=bookrez_1 
rreplace headers

/*------------------------------------------------------------------------
 Output Report
------------------------------------------------------------------------*/
viewpoint native;
  
  include "bookrez_i1.inc"

define
  file rate_link =access rateline_hf,
   set rateline_hf:commonid=bookrez_hf:commonid,
   one to one,zero fill on failure

  file tier_link =access tiers_hf,
   set tiers_hf:commonid=bookrez_hf:commonid,
   one to one,zero fill on failure

  file rev_link = access rev_sum_hf,
   set rev_sum_hf:commonid=bookrez_hf:commonid,
   one to one, zero fill on failure

  number num_days = if bookrez_hf:datein = null then if (rrm:datedue - rrm:dateout = 0) then 1 else (rrm:datedue - rrm:dateout) else
    if (rrm:datein - rrm:dateout = 0) then 1 else (rrm:datein - rrm:dateout)

  number tot_rev = rev_link:total_total + bookrez_hf:nettm

 list/title="Booked Reservation Report"/pageof/noheadings/nobanner/domain="bookrez_hf"
  bookrez_hf:locationcodeout/displaywidth=10/name="a"
  bookrez_hf:resnumber/displaywidth=10/name="b"
  bookrez_hf:dateadded/displaywidth=10/name="c"
  bookrez_hf:resstat/displaywidth=10/name="d"
  bookrez_hf:rentedasclass/displaywidth=8/name="e"
  bookrez_hf:renter/displaywidth=20/name="f"
  bookrez_hf:dateout/displaywidth=10/name="g"
  bookrez_hf:timeout/nototal/displaywidth=8/name="h"
  bookrez_hf:ratecode/displaywidth=8/name="i"
  bookrez_hf:rateplan/displaywidth=8/name="j"
  tot_rev/displaywidth=12/name="k"
  bookrez_hf:referralcode/displaywidth=8/name="l"
  bookrez_hf:referralname/displaywidth=10/name="m"
  bookrez_hf:addedbyemployeenumber/displaywidth=8/name="n"
  bookrez_hf:changedbyemployeenumber/displaywidth=8/name="o"

 include "detail_lines.cq"


sorted by
  bookrez_hf:sourcecode/count/total/heading="@"
  bookrez_hf:resnumber


top of bookrez_hf:sourcecode
   "Source: " + str(bookrez_hf:sourcecode)/newline

top of page
 "company_logo.jpg"/image/width=20/left/newline

 box/center/column=1
  if cLocgroup <> "" then "Location group: " + str(cLocGroup,100) 
  else "Locations: " + str(cLoc) /newline
  str(cdate)+" Range: " + str(dtsdate) + " to " +str(dtedate)/newline
  "Source: "+str(csource)/displaywidth=100/newline
  if ctaxes = "Y" then "Taxes Included in Total Revenue" else "Taxes Not Included in Total Revenue"/newline
  end box/newlines=4

  "Location"/align=a
  "Res#"/align=b
  "Date Entered" /align=c
  "Status" /align=d

  "Rented-As" /align=e
  "Renter" /align=f
  "Date Out"/align=g
  "Time Out"/align=h
  "Rate Code"/align=i
  "Rate Plan"/align=j
  "Total Revenue"/align=k
  "Referral"/align=l
  "Referral Name"/align=m
  "Added by"/align=n
  "Changed by"/align=o

  include "headers.cq"


  ""/newline

end of report
  ""/newline
  "Grand Total Records: "+str(count[bookrez_hf:resnumber,report])

;
replace bookrez.eq
run/parameterfile=bookrez_1 bookrez.eq
rrepl bookrez

/*------------------------------------------------------------------------
 Send report to screen
------------------------------------------------------------------------*/
view bookrez

/*------------------------------------------------------------------------
 Prepare for job submission to RW
------------------------------------------------------------------------*/

define
   string action      = "Report" -- Post or Report
   string rptdesc     = "Booked Reservation Report" -- Report Title
   string rptname1    = "bookrez" -- output file1
   string rptname2    = "" -- output file2 if applicable
   string rptname3    = "" -- output file3 if applicable
   string parfile     = "bookrez_1.par" -- parameter.par file
   string xmlname     = "" -- xml for posting if applicable
   string exportname  = "" -- posting export file if applicable
   string startdate   = ""  -- requires: include "parameter.inc" for this value if needed
   string enddate     = ""  -- requires: include "parameter.inc" for this value if needed


list/domain="dummy"/nodefaults/nobanner
action/newline
rptdesc/newline
rptname1/newline
rptname2/newline
rptname3/newline
parfile/newline
xmlname/newline
exportname/newline
startdate/newline
enddate/newline
;
run/parameterfile
rrepl rwsubmitpar
run/parameterfile rwsubmitjob.mf


